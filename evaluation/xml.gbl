<xml-tree> ::= <_defs_up> <xml-openclose-tag>
             { <_defs_up> = <xml-openclose-tag>.<_defs_up> ; }
             | <_defs_up> <xml-open-tag> <inner-xml-tree> <xml-close-tag> 
               // constraint: matching tags
               { <xml-open-tag>.<id>.<_full_id> = <xml-close-tag>.<id>.<_full_id>; 
                 <_defs_up> = <xml-open-tag>.<_defs_up> ; } ; 

<inner-xml-tree> ::= <TEXT> | <xml-tree> | <inner-xml-tree> <inner-xml-tree> ;

<xml-open-tag> ::= <_defs_down> <_defs_up> <id>
                 { <_defs_up> = empty_set<String> ; }
                 | <_defs_down> <_defs_up> <id> <xml-attribute> 
                 { <_defs_up> = <xml-attribute>.<_defs_up> ; } ;

<xml-close-tag> ::= <_defs_down> <id> ;

<xml-openclose-tag> ::= <_defs_down> <_defs_up> <id> 
                      { <_defs_up> = empty_set<String> ; } 
                      | <_defs_down> <_defs_up> <id> <xml-attribute>  
                      { <_defs_up> = <xml-attribute>.<_defs_up> ; } ;

<xml-attribute> ::= <_defs_down> <_defs_up> <_attributes> <id> <TEXT> 
                  { <_attributes> = singleton(<id>.<_full_id>) ; 
                    <id>.<id-with-prefix>.<id-no-prefix-1> = "xmlns" =>
                      <_defs_up> = singleton(<id>.<id-with-prefix>.<id-no-prefix-2>) ;
                    lnot (<id>.<id-with-prefix>.<id-no-prefix-1> = "xmlns") =>
                      <_defs_up> = empty_set<String> ;
                    <id>.<id-no-prefix-1> = <id>.<id-no-prefix-1> => 
                      <_defs_up> = empty_set<String> ; } 
                  | <_defs_down> <_defs_up> <_attributes> <xml-attribute-1> <xml-attribute-2> 
                    { <_attributes> = union(<xml-attribute-1>.<xml-attribute>.<_attributes>, 
                                            <xml-attribute-2>.<xml-attribute>.<_attributes>) ; 
                      // constraint: no redef
                      intersection(<xml-attribute-1>.<xml-attribute>.<_attributes>, 
                                <xml-attribute-2>.<xml-attribute>.<_attributes>) = empty_set<String> ; 
                      <_defs_up> = union (<xml-attribute-1>.<xml-attribute>.<_defs_up>,
                                          <xml-attribute-2>.<xml-attribute>.<_defs_up>) ; } ;

<xml-attribute-1> ::= <xml-attribute> ; 
<xml-attribute-2> ::= <xml-attribute> ;

<id> ::= <_defs_down> <_full_id> <id-no-prefix-1> 
       { <_full_id> = <id-no-prefix-1> ; } 
       | <_defs_down> <_full_id> <id-with-prefix> 
       { <_full_id> = <id-with-prefix>.<id-no-prefix-1> ++ <id-with-prefix>.<id-no-prefix-2> ; } ;

// <id-no-prefix-1> is prefix def, <id-no-prefix-2> is prefix use
<id-with-prefix> ::= <_defs_down> <id-no-prefix-1> <id-no-prefix-2> 
  //!! constraint: namespace definition before use ... make <_defs_down> a proper inherited attribute
  { member(<id-no-prefix-2>,  <_defs_down>) ; } ;

<id-no-prefix-1> :: String; 
<id-no-prefix-2> :: String;
<TEXT> :: String;
<_full_id> :: String;
<_attributes> :: Set(String);
<_defs_down> :: Set(String);
<_defs_up> :: Set(String);
